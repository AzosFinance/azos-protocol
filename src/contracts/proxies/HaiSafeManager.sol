// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.19;

import {SAFEHandler} from '@contracts/proxies/SAFEHandler.sol';
import {ISAFEEngine} from '@interfaces/ISAFEEngine.sol';
import {ILiquidationEngine} from '@interfaces/ILiquidationEngine.sol';

import {Math} from '@libraries/Math.sol';
import {EnumerableSet} from '@openzeppelin/utils/structs/EnumerableSet.sol';
import {Assertions} from '@libraries/Assertions.sol';

/**
 * @title  HaiSafeManager
 * @notice This contract acts as interface to the SAFEEngine, facilitating the management of SAFEs
 * @dev    This contract is meant to be used by users that interact with the protocol through a proxy contract
 */
contract HaiSafeManager {
  using Math for uint256;
  using EnumerableSet for EnumerableSet.UintSet;
  using Assertions for address;

  struct SAFEData {
    // Address of the safe owner
    address owner;
    // Address of the safe handler
    address safeHandler;
    // Collateral type of the safe
    bytes32 collateralType;
  }

  // --- Registry ---

  /// @notice Address of the SAFEEngine
  address public safeEngine;

  // --- Data ---

  /// @notice Nonce used to generate safe ids (autoincremental)
  uint256 internal _safeId;
  /// @notice Mapping of user addresses to their enumerable set of safes
  mapping(address _safeOwner => EnumerableSet.UintSet) private _usrSafes;
  /// @notice Mapping of user addresses to their enumerable set of safes per collateral type
  mapping(address _safeOwner => mapping(bytes32 _cType => EnumerableSet.UintSet)) private _usrSafesPerCollat;
  /// @notice Mapping of safe ids to their data
  mapping(uint256 _safeId => SAFEData) internal _safeData;
  /// @notice Mapping of owner and safe permissions to a caller permissions
  mapping(address _owner => mapping(uint256 _safeId => mapping(address _caller => uint256 _ok))) public safeCan;
  /// @notice Mapping of handler to a caller permissions
  mapping(address _safeHandler => mapping(address _caller => uint256 _ok)) public handlerCan;

  // --- Errors ---

  /// @notice Throws if the provided address is null
  error ZeroAddress();
  /// @notice Throws when trying to call a function not allowed for a given safe
  error SafeNotAllowed();
  /// @notice Throws when trying to call a function not allowed for a given handler
  error HandlerNotAllowed();
  /// @notice Throws when trying to transfer safe ownership to the current owner
  error AlreadySafeOwner();
  /// @notice Throws when trying to move a safe to another one with different collateral type
  error CollateralTypesMismatch();

  // --- Events ---

  /// @notice Emitted when calling allowSAFE with the sender address and the method arguments
  event AllowSAFE(address indexed _sender, uint256 indexed _safe, address _usr, uint256 _ok);
  /// @notice Emitted when calling allowHandler with the sender address and the method arguments
  event AllowHandler(address indexed _sender, address _usr, uint256 _ok);
  /// @notice Emitted when calling transferSAFEOwnership with the sender address and the method arguments
  event TransferSAFEOwnership(address indexed _sender, uint256 indexed _safe, address _dst);
  /// @notice Emitted when calling openSAFE with the sender address and the method arguments
  event OpenSAFE(address indexed _sender, address indexed _own, uint256 indexed _safe);
  /// @notice Emitted when calling modifySAFECollateralization with the sender address and the method arguments
  event ModifySAFECollateralization(
    address indexed _sender, uint256 indexed _safe, int256 _deltaCollateral, int256 _deltaDebt
  );
  /// @notice Emitted when calling transferCollateral with the sender address and the method arguments
  event TransferCollateral(address indexed _sender, uint256 indexed _safe, address _dst, uint256 _wad);
  /// @notice Emitted when calling transferCollateral (specifying cType) with the sender address and the method arguments
  event TransferCollateral(address indexed _sender, bytes32 _cType, uint256 indexed _safe, address _dst, uint256 _wad);
  /// @notice Emitted when calling transferInternalCoins with the sender address and the method arguments
  event TransferInternalCoins(address indexed _sender, uint256 indexed _safe, address _dst, uint256 _rad);
  /// @notice Emitted when calling quitSystem with the sender address and the method arguments
  event QuitSystem(address indexed _sender, uint256 indexed _safe, address _dst);
  /// @notice Emitted when calling enterSystem with the sender address and the method arguments
  event EnterSystem(address indexed _sender, address _src, uint256 indexed _safe);
  /// @notice Emitted when calling moveSAFE with the sender address and the method arguments
  event MoveSAFE(address indexed _sender, uint256 indexed _safeSrc, uint256 indexed _safeDst);
  /// @notice Emitted when calling protectSAFE with the sender address and the method arguments
  event ProtectSAFE(address indexed _sender, uint256 indexed _safe, address _liquidationEngine, address _saviour);

  // --- Modifiers ---

  /**
   * @notice Checks if the sender is the owner of the safe or the safe has permissions to call the function
   * @param  _safe Id of the safe to check if msg.sender has permissions for
   */
  modifier safeAllowed(uint256 _safe) {
    address _owner = _safeData[_safe].owner;
    if (msg.sender != _owner && safeCan[_owner][_safe][msg.sender] == 0) revert SafeNotAllowed();
    _;
  }

  /**
   * @notice Checks if the sender is the safe handler has permissions to call the function
   * @param  _handler Address of the handler to check if msg.sender has permissions for
   */
  modifier handlerAllowed(address _handler) {
    if (msg.sender != _handler && handlerCan[_handler][msg.sender] == 0) revert HandlerNotAllowed();
    _;
  }

  // --- Init ---

  /**
   * @param  _safeEngine Address of the SAFEEngine
   */
  constructor(address _safeEngine) {
    safeEngine = _safeEngine.assertNonNull();
  }

  // --- Getters ---

  /**
   * @notice Getter for the list of safes owned by a user
   * @param  _usr Address of the user
   * @return _safes List of safe ids owned by the user
   */
  function getSafes(address _usr) external view returns (uint256[] memory _safes) {
    _safes = _usrSafes[_usr].values();
  }

  /**
   * @notice Getter for the list of safes owned by a user for a given collateral type
   * @param  _usr Address of the user
   * @param  _cType Bytes32 representation of the collateral type
   * @return _safes List of safe ids owned by the user for the given collateral type
   */
  function getSafes(address _usr, bytes32 _cType) external view returns (uint256[] memory _safes) {
    _safes = _usrSafesPerCollat[_usr][_cType].values();
  }

  /**
   * @notice Getter for the details of the safes owned by a user
   * @param  _usr Address of the user
   * @return _safes List of safe ids owned by the user
   * @return _safeHandlers List of safe handlers addresses owned by the user
   * @return _cTypes List of collateral types of the safes owned by the user
   */
  function getSafesData(address _usr)
    external
    view
    returns (uint256[] memory _safes, address[] memory _safeHandlers, bytes32[] memory _cTypes)
  {
    _safes = _usrSafes[_usr].values();
    _safeHandlers = new address[](_safes.length);
    _cTypes = new bytes32[](_safes.length);
    for (uint256 _i; _i < _safes.length; _i++) {
      _safeHandlers[_i] = _safeData[_safes[_i]].safeHandler;
      _cTypes[_i] = _safeData[_safes[_i]].collateralType;
    }
  }

  /**
   * @notice Getter for the details of a SAFE
   * @param  _safe Id of the SAFE
   * @return _sData Struct with the safe data
   */
  function safeData(uint256 _safe) external view returns (SAFEData memory _sData) {
    _sData = _safeData[_safe];
  }

  // --- Methods ---

  /**
   * @notice Allow/disallow a user address to manage the safe
   * @param  _safe Id of the SAFE
   * @param  _usr Address of the user to allow/disallow
   * @param  _ok Boolean state to allow/disallow
   */
  function allowSAFE(uint256 _safe, address _usr, uint256 _ok) external safeAllowed(_safe) {
    address _owner = _safeData[_safe].owner;
    safeCan[_owner][_safe][_usr] = _ok;
    emit AllowSAFE(msg.sender, _safe, _usr, _ok);
  }

  /**
   * @notice Allow/disallow a handler address to manage the safe
   * @param  _usr Address of the user to allow/disallow
   * @param  _ok Boolean state to allow/disallow
   */
  function allowHandler(address _usr, uint256 _ok) external {
    handlerCan[msg.sender][_usr] = _ok;
    emit AllowHandler(msg.sender, _usr, _ok);
  }

  /**
   * @notice Open a new safe for a user address
   * @param  _cType Bytes32 representation of the collateral type
   * @param  _usr Address of the user to open the safe for
   * @return _id Id of the new SAFE
   */
  function openSAFE(bytes32 _cType, address _usr) external returns (uint256 _id) {
    if (_usr == address(0)) revert ZeroAddress();

    ++_safeId;
    address _safeHandler = address(new SAFEHandler(safeEngine));

    _safeData[_safeId] = SAFEData({owner: _usr, safeHandler: _safeHandler, collateralType: _cType});

    _usrSafes[_usr].add(_safeId);
    _usrSafesPerCollat[_usr][_cType].add(_safeId);

    emit OpenSAFE(msg.sender, _usr, _safeId);
    return _safeId;
  }

  /**
   * @notice Transfer the ownership of a safe to a dst address
   * @param  _safe Id of the SAFE
   * @param  _dst Address of the dst address
   */
  function transferSAFEOwnership(uint256 _safe, address _dst) external safeAllowed(_safe) {
    if (_dst == address(0)) revert ZeroAddress();
    SAFEData memory _sData = _safeData[_safe];
    if (_dst == _sData.owner) revert AlreadySafeOwner();

    _usrSafes[_sData.owner].remove(_safe);
    _usrSafesPerCollat[_sData.owner][_sData.collateralType].remove(_safeId);

    _usrSafes[_dst].add(_safe);
    _usrSafesPerCollat[_dst][_sData.collateralType].add(_safeId);

    _safeData[_safe].owner = _dst;

    emit TransferSAFEOwnership(msg.sender, _safe, _dst);
  }

  /**
   * @notice Modify a SAFE's collateralization ratio while keeping the generated COIN or collateral freed in the safe handler address
   * @param  _safe Id of the SAFE
   * @param  _deltaCollateral Delta of collateral to add/remove [wad]
   * @param  _deltaDebt Delta of debt to add/remove [wad]
   */
  function modifySAFECollateralization(
    uint256 _safe,
    int256 _deltaCollateral,
    int256 _deltaDebt
  ) external safeAllowed(_safe) {
    SAFEData memory _sData = _safeData[_safe];
    ISAFEEngine(safeEngine).modifySAFECollateralization(
      _sData.collateralType, _sData.safeHandler, _sData.safeHandler, _sData.safeHandler, _deltaCollateral, _deltaDebt
    );
    emit ModifySAFECollateralization(msg.sender, _safe, _deltaCollateral, _deltaDebt);
  }

  /**
   * @notice Transfer wad amount of safe collateral from the safe address to a dst address
   * @param  _safe Id of the SAFE
   * @param  _dst Address of the dst address
   * @param  _wad Amount of collateral to transfer [wad]
   */
  function transferCollateral(uint256 _safe, address _dst, uint256 _wad) external safeAllowed(_safe) {
    SAFEData memory _sData = _safeData[_safe];
    ISAFEEngine(safeEngine).transferCollateral(_sData.collateralType, _sData.safeHandler, _dst, _wad);
    emit TransferCollateral(msg.sender, _safe, _dst, _wad);
  }

  /**
   * @notice Transfer wad amount of any type of collateral from the safe address to a dst address
   * @param  _cType Bytes32 representation of the collateral type
   * @param  _safe Id of the SAFE
   * @param  _dst Address of the dst address
   * @param  _wad Amount of collateral to transfer [wad]
   * @dev    This function has the purpose to take away collateral from the system that doesn't correspond to the safe but was sent there wrongly.
   */
  function transferCollateral(bytes32 _cType, uint256 _safe, address _dst, uint256 _wad) external safeAllowed(_safe) {
    SAFEData memory _sData = _safeData[_safe];
    ISAFEEngine(safeEngine).transferCollateral(_cType, _sData.safeHandler, _dst, _wad);
    emit TransferCollateral(msg.sender, _cType, _safe, _dst, _wad);
  }

  /**
   * @notice Transfer an amount of COIN from the safe address to a dst address [rad]
   * @param  _safe Id of the SAFE
   * @param  _dst Address of the dst address
   * @param  _rad Amount of COIN to transfer [rad]
   */
  function transferInternalCoins(uint256 _safe, address _dst, uint256 _rad) external safeAllowed(_safe) {
    SAFEData memory _sData = _safeData[_safe];
    ISAFEEngine(safeEngine).transferInternalCoins(_sData.safeHandler, _dst, _rad);
    emit TransferInternalCoins(msg.sender, _safe, _dst, _rad);
  }

  /**
   * @notice Quit the system, migrating the safe (lockedCollateral, generatedDebt) to a different dst handler
   * @param  _safe Id of the SAFE
   * @param  _dst Address of the dst handler
   */
  function quitSystem(uint256 _safe, address _dst) external safeAllowed(_safe) handlerAllowed(_dst) {
    SAFEData memory _sData = _safeData[_safe];
    ISAFEEngine.SAFE memory _safeInfo = ISAFEEngine(safeEngine).safes(_sData.collateralType, _sData.safeHandler);
    int256 _deltaCollateral = _safeInfo.lockedCollateral.toInt();
    int256 _deltaDebt = _safeInfo.generatedDebt.toInt();
    ISAFEEngine(safeEngine).transferSAFECollateralAndDebt(
      _sData.collateralType, _sData.safeHandler, _dst, _deltaCollateral, _deltaDebt
    );

    // Remove safe from owner's list (notice it doesn't erase safe ownership)
    _usrSafes[_sData.owner].remove(_safe);
    _usrSafesPerCollat[_sData.owner][_sData.collateralType].remove(_safe);
    emit QuitSystem(msg.sender, _safe, _dst);
  }

  /**
   * @notice Enter the system, migrating the safe (lockedCollateral, generatedDebt) from a src handler to the safe handler
   * @param  _src Address of the src handler
   * @param  _safe Id of the SAFE
   */
  function enterSystem(address _src, uint256 _safe) external handlerAllowed(_src) safeAllowed(_safe) {
    SAFEData memory _sData = _safeData[_safe];
    ISAFEEngine.SAFE memory _safeInfo = ISAFEEngine(safeEngine).safes(_sData.collateralType, _sData.safeHandler);
    int256 _deltaCollateral = _safeInfo.lockedCollateral.toInt();
    int256 _deltaDebt = _safeInfo.generatedDebt.toInt();
    ISAFEEngine(safeEngine).transferSAFECollateralAndDebt(
      _sData.collateralType, _src, _sData.safeHandler, _deltaCollateral, _deltaDebt
    );
    emit EnterSystem(msg.sender, _src, _safe);
  }

  /**
   * @notice Move a position from safeSrc handler to the safeDst handler
   * @param  _safeSrc Id of the source SAFE
   * @param  _safeDst Id of the destination SAFE
   */
  function moveSAFE(uint256 _safeSrc, uint256 _safeDst) external safeAllowed(_safeSrc) safeAllowed(_safeDst) {
    SAFEData memory _srcData = _safeData[_safeSrc];
    SAFEData memory _dstData = _safeData[_safeDst];
    if (_srcData.collateralType != _dstData.collateralType) revert CollateralTypesMismatch();
    ISAFEEngine.SAFE memory _safeInfo = ISAFEEngine(safeEngine).safes(_srcData.collateralType, _srcData.safeHandler);
    int256 _deltaCollateral = _safeInfo.lockedCollateral.toInt();
    int256 _deltaDebt = _safeInfo.generatedDebt.toInt();
    ISAFEEngine(safeEngine).transferSAFECollateralAndDebt(
      _srcData.collateralType, _srcData.safeHandler, _dstData.safeHandler, _deltaCollateral, _deltaDebt
    );

    // Remove safe from owner's list (notice it doesn't erase safe ownership)
    _usrSafes[_srcData.owner].remove(_safeSrc);
    _usrSafesPerCollat[_srcData.owner][_srcData.collateralType].remove(_safeSrc);
    emit MoveSAFE(msg.sender, _safeSrc, _safeDst);
  }

  /**
   * @notice Add a safe to the user's list of safes (doesn't set safe ownership)
   * @param  _safe Id of the SAFE
   * @dev    This function is meant to allow the user to add a safe to their list (if it was previously removed)
   */
  function addSAFE(uint256 _safe) external {
    SAFEData memory _sData = _safeData[_safe];
    _usrSafes[msg.sender].add(_safe);
    _usrSafesPerCollat[msg.sender][_sData.collateralType].add(_safe);
  }

  /**
   * @notice Remove a safe from the user's list of safes (doesn't erase safe ownership)
   * @param  _safe Id of the SAFE
   * @dev    This function is meant to allow the user to remove a safe from their list (if it was added against their will)
   */
  function removeSAFE(uint256 _safe) external safeAllowed(_safe) {
    SAFEData memory _sData = _safeData[_safe];
    _usrSafes[_sData.owner].remove(_safe);
    _usrSafesPerCollat[_sData.owner][_sData.collateralType].remove(_safe);
  }

  /**
   * @notice Choose a safe saviour inside LiquidationEngine for the SAFE
   * @param  _safe Id of the SAFE
   * @param  _liquidationEngine Address of the LiquidationEngine
   * @param  _saviour Address of the saviour
   */
  function protectSAFE(uint256 _safe, address _liquidationEngine, address _saviour) external safeAllowed(_safe) {
    SAFEData memory _sData = _safeData[_safe];
    ILiquidationEngine(_liquidationEngine).protectSAFE(_sData.collateralType, _sData.safeHandler, _saviour);
    emit ProtectSAFE(msg.sender, _safe, _liquidationEngine, _saviour);
  }
}
